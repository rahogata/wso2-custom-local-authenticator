plugins {
    id 'java'
    id 'eclipse'
    id 'biz.aQute.bnd.builder' version '4.0.0'
}

group = 'org.wso2.custom.authenticator'
version = '1.0.0'
description = 'WSO2 Carbon - BasicAuth Custom Authenticator'

/* To avoid warning,
 * POM relocation to an other version number is not fully supported in Gradle : xml-apis:xml-apis:2.0.2 relocated to xml-apis:xml-apis:1.0.b2
 */
configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.0.b2'
    }
}

repositories {
    mavenLocal()
    maven {
        url = 'http://maven.wso2.org/nexus/content/groups/wso2-public/'
    }

    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

dependencies {
    implementation 'org.wso2.carbon:org.wso2.carbon.utils:4.4.11'
    implementation 'org.wso2.carbon:org.wso2.carbon.logging:4.4.11'
    implementation 'org.wso2.carbon.identity.framework:org.wso2.carbon.identity.application.authentication.framework:5.7.5'
}

import aQute.bnd.gradle.Bundle
task uberJar(type: Bundle) {
    manifest {
        attributes(
            '-dsannotations': '*',
            '-dsannotations-options': 'version;minimum=1.2.0',
            'Import-Package': "javax.servlet"
                              + ",javax.servlet.http"
                              + ",*;resolution:=optional",
            'Export-Package': "!org.wso2.custom.authenticator.local.internal"
                              + ",org.wso2.custom.authenticator.local.*;"
                              + 'version="1.0.0"',
            //'Conditional-Package': '',
            //'Include-Resource': ''
        )
    }
    
    with jar
}
